/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */
apply plugin: 'base'
apply plugin: 'maven'

def hibernateVersion = '5.0.12.Final'
def wildflyVersion = '11.0.0.Final'

ext {
	// Exact ORM version, e.g. "5.1.1.Final"
	// Just the minor ORM version, e.g. "5.1"; Is used as an alias for the exact version
	minorSlot = hibernateVersion.substring( 0, hibernateVersion.indexOf( ".", hibernateVersion.indexOf( "." ) + 1 ) )

	majorWildflyVersion = wildflyVersion.substring( 0, wildflyVersion.indexOf( "." ) )

	// directory for building the ZIP file from
	modulesDirectory = "$buildDir/hibernate-orm-modules"
}

configurations {
	wildflyDist
}

dependencies {
	wildflyDist "org.wildfly:wildfly-dist:${wildflyVersion}@zip"

	testCompile "org.hibernate:hibernate-core:${hibernateVersion}"
	testCompile "org.hibernate:hibernate-entitymanager:${hibernateVersion}"

	testCompile libraries.junit
	testCompile libraries.arquillian_junit_container
	testCompile libraries.arquillian_protocol_servlet

	testCompile libraries.shrinkwrap_descriptors_api_javaee
	testCompile libraries.shrinkwrap_descriptors_impl_javaee

	testCompile( 'org.wildfly.arquillian:wildfly-arquillian-container-managed:2.1.1.Final' ) {
		exclude group: 'org.picketbox', module: 'picketbox'
	}
}


// Unzip Wildfly Dist
task extractWildFly( type: Copy ) {
	from {
		configurations.wildflyDist.collect { zipTree( it ) }
	}
	into "$buildDir/"
}

// Replace properties in arquillian.xml; Actually this should be done by means of configuring
// the processTestResourcesTask itself, but while that works for resources in src/main/resources,
// the same failed for src/test/resources; I reckon it's a bug in Gradle
task filterArquillianXml( dependsOn: processTestResources, type: Copy ) {
	into( buildDir.getName() + '/resources/test' )
	expand( buildDir: buildDir.getName(), wildflyVersion: wildflyVersion )

	from 'src/test/resources'
}

task copyResourcesToIntelliJOutFolder(dependsOn: filterArquillianXml) {
	doLast {
		copy {
			from "$buildDir/resources/test"
			into 'out/test/resources'
		}
	}
}

test.dependsOn extractWildFly
test.dependsOn filterArquillianXml, copyResourcesToIntelliJOutFolder
